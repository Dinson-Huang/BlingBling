apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'//kapt3插件
apply plugin: 'androidx.navigation.safeargs'//navigation 传递参数插件
apply plugin: 'com.huawei.agconnect'
apply from: 'andres-guard.gradle'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "dinson.customview"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        multiDexEnabled true
        testOptions {
            unitTests.returnDefaultValues = true
        }
        javaCompileOptions { annotationProcessorOptions { includeCompileClasspath = true } }
        ndk {
            abiFilters 'armeabi-v7a'
            //, 'arm64-v8a', 'x86', 'x86_64'//, 'armeabi','armeabi-v7a', 'arm64-v8a'
        }
        renderscriptSupportModeEnabled true    // Enable RS support

    }
    /*打包 gradlew resguardRelease*/
    signingConfigs {
        app {
            Properties prop = new Properties()
            prop.load(project.rootProject.file('local.properties').newDataInputStream())

            storeFile file(prop['keystore.path'])
            storePassword prop['keystore.pwd']
            keyAlias prop['keystore.alias']
            keyPassword prop['keystore.aliaspwd']
        }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.buildType.name == "release") {
                outputFileName = "BlingBling_${new Date().format("MMddHHmm")}_${variant.buildType.name}.apk"
            } else {
                outputFileName = "BlingBling_${variant.buildType.name}.apk"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true      //开启代码混淆
            shrinkResources true    //开启shrinkResources去除无用资源
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.app
            debuggable true
        }
        debug {
            signingConfig signingConfigs.app
        }
    }
    flavorDimensions "default"
    productFlavors {
        BlingBling {
            Properties tempPro = new Properties()
            tempPro.load(new FileInputStream(file("../constant.properties")))
            buildConfigField 'String', 'QINIU_URL', tempPro['QINIU_URL']
            buildConfigField 'String', 'BMOB_APPID', tempPro['BMOB_APPID']
            buildConfigField 'String', 'BMOB_MASTER_KEY', tempPro['BMOB_MASTER_KEY']
            buildConfigField 'String', 'BMOB_REST_API_KEY', tempPro['BMOB_REST_API_KEY']
            buildConfigField 'String', 'HUAWEI_API_KEY', tempPro['HUAWEI_API_KEY']
        }
    }
    //kotlin序列化
    androidExtensions {
        experimental = true
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    dataBinding.enabled = true

    /*做七牛云上传图片时。图片选择添加，不用可删除*/
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/proguard/coroutines.pro'
    }
    lintOptions {
        disable 'GoogleAppIndexingWarning'
        baseline file("lint-baseline.xml") // your choice of filename/path here
    }
    externalNativeBuild {
        cmake {
            path file('CMakeLists.txt')
        }
    }
    //kotlin序列化
    androidExtensions {
        experimental = true
    }
}

dependencies {
    implementation files('libs/AMap_Location_V3.6.0_20170918.jar')
    implementation project(path: ':blingbase')
    //implementation 'com.dinson.bling:blingsocial-alipay:1.0.4'
    /**
     * 官方库
     */
    /*androidx*/
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    implementation 'com.google.android:flexbox:0.2.2'/*布局自动换行*/
    implementation 'com.google.vr:sdk-panowidget:1.80.0'/*googleVR*/
    implementation 'com.google.android.gms:play-services-maps:17.0.0'/*google地图*/
    implementation 'com.google.android.gms:play-services-location:17.1.0'/*google定位*/
    annotationProcessor 'androidx.annotation:annotation:1.1.0'/*androidx 注解兼容*/

    /*在Kotlin中DataBinding的注解,kapt是 Kotlin内置的注解处理器*/
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    //implementation "org.jetbrains.anko:anko-commons:$anko_version"/*kotlin扩展类*/

    /*RX全家桶*/
    implementation 'io.reactivex.rxjava2:rxkotlin:2.0.2'/*RxKotlin*/
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0' /*RXBinding*/
    implementation "com.trello.rxlifecycle2:rxlifecycle-components:$rxlifecycle"/*RXLifecycle*/

    implementation 'org.greenrobot:greendao:3.3.0'/*数据库*/

    /**
     * 自定义控件
     */
    implementation 'com.caverock:androidsvg:1.2.2-beta-1'/*svg图片支持*/
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar' /*实时权限*/
    implementation 'com.qiniu:qiniu-java-sdk:7.2.1'/*七牛云*/
    implementation 'com.just.agentweb:agentweb:4.1.3'/*WebView框架*/
    implementation 'com.facebook.rebound:rebound:0.3.8'/*Facebook闪光控件*/
    implementation 'com.rmtheis:tess-two:9.0.0'/*ORC图片识别库*/
    implementation 'org.jsoup:jsoup:1.11.2'/*网页解析*/
    implementation 'com.shuyu:GSYVideoPlayer:7.1.4'/*视频框架*/
    implementation 'com.github.mmin18:realtimeblurview:1.2.1'/*高斯模糊控件*/
    implementation 'com.github.promeg:tinypinyin:2.0.3'// TinyPinyin核心包，约80KB
    implementation 'com.github.JmStefanAndroid:Mu5ViewPager:1.2'
    implementation 'com.github.LuckSiege.PictureSelector:picture_library:v2.5.3'/*图片选择框架*/
    implementation 'com.huawei.hms:ml-computer-vision-ocr:2.0.1.300'/*华为识别库*/
    implementation 'com.huawei.hms:ml-computer-vision-ocr-cn-model:2.0.1.300'// 引入中英文文字识别模型
}
