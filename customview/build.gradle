apply plugin: 'com.android.application'
apply plugin: 'AndResGuard'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

Properties prop = new Properties()
prop.load(new FileInputStream(file("../signing.properties")))

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.2"

    defaultConfig {
        applicationId "dinson.customview"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        multiDexEnabled true
        testOptions {
            unitTests.returnDefaultValues = true
        }
        javaCompileOptions { annotationProcessorOptions { includeCompileClasspath = true } }
        ndk {
            abiFilters 'armeabi-v7a', 'x86'//, 'x86_64', 'armeabi','armeabi-v7a', 'arm64-v8a'
        }
    }
    /*打包 gradlew resguardRelease*/
    signingConfigs {
        app {
            storeFile file(prop['KEYSTORE_FILEPATH'])
            storePassword prop['STORE_PASSWORD']
            keyAlias prop['KEY_ALIAS']
            keyPassword prop['KEY_PASSWORD']
        }
    }
    buildTypes {
        release {
            minifyEnabled true      //开启代码混淆
            shrinkResources true    //开启shrinkResources去除无用资源
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.app
            debuggable false
        }
        debug {
            signingConfig signingConfigs.app
        }
    }


    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}

dependencies {
    implementation files('libs/AMap_Location_V3.6.0_20170918.jar')
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:support-dynamic-animation:$support_version"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"/*kotlin*/
    implementation "org.jetbrains.anko:anko-commons:$anko_version"/*kotlin扩展类*/

    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'/*RXJava*/
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'/*Retrofit*/
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.2.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    implementation 'com.google.vr:sdk-panowidget:1.80.0'/*googleVR*/
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'/*实时权限*/

    implementation 'com.github.bumptech.glide:glide:4.0.0-RC1'/*Glide*/
    implementation 'com.github.bumptech.glide:compiler:4.0.0-RC1'
    implementation 'com.caverock:androidsvg:1.2.2-beta-1'/*svg图片支持*/

    implementation 'tv.danmaku.ijk.media:ijkplayer-java:0.8.4'/*IJK框架*/
    implementation 'tv.danmaku.ijk.media:ijkplayer-armv7a:0.8.4'

    implementation 'com.facebook.rebound:rebound:0.3.8'/*Facebook闪光控件*/
    implementation 'org.greenrobot:greendao:3.2.2'/*数据库*/
    implementation 'org.jsoup:jsoup:1.10.3'/*网页解析*/

    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
}


andResGuard {
    // mappingFile = file("./resource_mapping.txt")
    mappingFile = null
    // 当你使用v2签名的时候，7zip压缩是无法生效的。
    use7zip = true
    useSign = true
    // 打开这个开关，会keep住所有资源的原始路径，只混淆资源的名字
    keepRoot = false
    whiteList = [
            // for your icon
            "R.drawable.icon",
            // for fabric
            "R.string.com.crashlytics.*",
            // for google-services
            "R.string.google_app_id",
            "R.string.gcm_defaultSenderId",
            "R.string.default_web_client_id",
            "R.string.ga_trackingId",
            "R.string.firebase_database_url",
            "R.string.google_api_key",
            "R.string.google_crash_reporting_api_key"
    ]
    compressFilePattern = [
            "*.png",
            "*.jpg",
            "*.jpeg",
            "*.gif",
            "resources.arsc"
    ]
    sevenzip {
        artifact = 'com.tencent.mm:SevenZip:1.2.3'
        //path = "/usr/local/bin/7za"
    }
}
repositories {
    mavenCentral()
    google()
}
